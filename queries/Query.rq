PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX mf:      <http://www.w3.org/2001/sw/DataAccess/tests/test-manifest#>
PREFIX dawgt:   <http://www.w3.org/2001/sw/DataAccess/tests/test-dawg#>
PREFIX qt:      <http://www.w3.org/2001/sw/DataAccess/tests/test-query#>
PREFIX ut:      <http://www.w3.org/2009/sparql/tests/test-update#>
PREFIX sd:      <http://www.w3.org/ns/sparql-service-description#>
PREFIX ent:     <http://www.w3.org/ns/entailment/RDF>
PREFIX rs:      <http://www.w3.org/2001/sw/DataAccess/tests/result-set#>

SELECT DISTINCT ?type ?name ?query ?result ?data ?test (GROUP_CONCAT(DISTINCT COALESCE(STR(?feature), ""); SEPARATOR=";") AS ?featureList) ?comment ?approval (GROUP_CONCAT(DISTINCT COALESCE(STR(?approvedBy), ""); SEPARATOR=";") AS ?approvedByList) ?regime (GROUP_CONCAT(DISTINCT COALESCE(STR(?actionGraphData), ""); SEPARATOR=";") AS ?graphDatas) ?resultgraphData
WHERE {
    ?test rdf:type mf:QueryEvaluationTest .
    BIND ("QueryEvaluationTest" AS ?type) .
    ?test mf:action ?action .
    ?action qt:query ?query .
    OPTIONAL {?action qt:data ?data .}
    OPTIONAL {?action sd:entailmentRegime ?regime .}
    OPTIONAL {?action qt:graphData  ?actionGraphData .}
    ?test mf:result ?result .
    OPTIONAL {?test mf:name ?name .}
    OPTIONAL {?test mf:feature ?feature .}
    OPTIONAL {?test rdfs:comment ?comment .}
    OPTIONAL {?test dawgt:approval ?approval .}
    OPTIONAL {?test dawgt:approvedBy  ?approvedBy .}
}
GROUP BY ?type ?name ?query ?result ?data ?test ?comment ?approval ?regime ?resultgraphData
